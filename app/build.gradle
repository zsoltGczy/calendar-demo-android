apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

apply from: 'utils.gradle'

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))
android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.gzslt.calendardemo"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "EVENTS_API_KEY", "\"" + localProperties['eventsApiKey'] + "\""

        // Local database
        buildConfigField "int", 'DATABASE_VERSION', '1'
        buildConfigField "String", 'DATABASE_NAME', doubleQuotes("default")

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }

            javacOptions {
                // These options are normally set automatically via the Hilt Gradle plugin, but we
                // set them manually to workaround a bug in the Kotlin 1.5.20
                // https://github.com/google/dagger/issues/2684
                option("-Adagger.fastInit=ENABLED")
                option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    buildFeatures {
        viewBinding true
    }
}

configurations {
    ktlint
}

dependencies {

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$rootProject.desugarVersion"

    implementation "androidx.appcompat:appcompat:$rootProject.supportLibraryVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintlayoutVersion"
    implementation "androidx.core:core-ktx:$rootProject.ktxVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigationVersion"
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    implementation "com.github.kizitonwose:CalendarView:$rootProject.calendarViewVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"
    implementation "com.google.dagger:hilt-android:$rootProject.hiltVersion"
    implementation "com.squareup.moshi:moshi-adapters:$rootProject.moshiVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.kotlinVersion"

    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.androidXTestExtJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.androidXTestEspressoVersion"

    ktlint "com.pinterest:ktlint:$rootProject.ktlintVersion"

    kapt "androidx.room:room-compiler:$rootProject.roomVersion"
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.hiltVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$rootProject.moshiVersion"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt", "--reporter=checkstyle,output=${buildDir}/reports/ktlint.xml"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt", "--reporter=checkstyle,output=${buildDir}/reports/ktlint.xml"
}

// Android Studio needs to be restarted after running this task
task addKtLintStyle(type: JavaExec, group: "formatting") {
    description = "Adds The KtLint Style To Your IDE"
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--android", "applyToIDEA", "-y"
    // Point to the root directory because this task needs access to the .idea directory
    workingDir(getRootDir())
}
